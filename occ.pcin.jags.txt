
model {

 ##prior
 #prior for psi
 beta0.pcin ~ dnorm(0,0.01)
 beta.cwd.pcin ~ dnorm (0, 0.01)
 beta.canop.pcin ~ dnorm (0, 0.01)
 
 #prior for p
 alpha0.pcin ~ dnorm(0,0.01)
 alpha.precip.pcin ~ dnorm(0,0.01)
 alpha.cwd.pcin ~ dnorm(0,0.01)
 
 
  ##likelihood
 ##first loop over nsites
 for (i in 1:nsites) {
    ##True occupancy z at site i (biological process)
    z[i] ~ dbern(psi[i])

    ##linear predictor of psi
    logit(psi[i]) <- beta0.pcin + beta.cwd.pcin * CWD.STD.occ[i] 
    + beta.canop.pcin * Canop[i]
    

    ##second loop over nvisits
    for (j in 1:nvisits) {
        ##linear predictor of p
        logit(p[i, j]) <- alpha0.pcin + alpha.precip.pcin * Precip[i,j] 
        + alpha.cwd.pcin * CWD.STD.det[i]
    
        ##p = 0 if site not occupied
        eff.p[i, j] <- z[i] * p[i, j]

        ##detection at i j (observation process)
        y[i, j] ~ dbern(eff.p[i, j])
    
      }
 }

## derived parameter
 ##number of occupied sites among nsites
 finiteOcc <- sum(z[])
}


